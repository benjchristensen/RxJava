apply plugin: 'osgi'

dependencies {
    core project(':rxjava-core')
    provided project(':rxjava-core')
    provided project(':language-adaptors:codegen')
    compile 'org.jruby:jruby:1.6+'
    provided 'junit:junit-dep:4.10'
    provided 'org.mockito:mockito-core:1.8.5'
}

task createAdaptedObservable(type: JavaExec) { 
    main = 'rx.codegen.ClassPathBasedRunner'
    classpath = sourceSets.main.runtimeClasspath + configurations.provided
    args = ["JRuby", codeGenOutputDir]

    inputs.files(sourceSets.main.runtimeClasspath)
    outputs.dir(codeGenOutputDir)
}

tasks.test {
    dependsOn(createAdaptedObservable)

    //Reorders the classpath so that the newly-create Observables win
    classpath = createAdaptedObservable.outputs.files + sourceSets.test.runtimeClasspath
}

tasks.jar {
    dependsOn(createAdaptedObservable)

    from (zipTree(configurations.core.singleFile)) {
        exclude "rx/Observable.class"
        exclude "rx/observables/BlockingObservable.class"
    }
    from(codeGenOutputDir)

    exclude('**/*$UnitTest*')

    manifest {
        name = 'rxjava-jruby'
        instruction 'Bundle-Vendor', 'Netflix'
        instruction 'Bundle-DocURL', 'https://github.com/Netflix/RxJava'
        instruction 'Import-Package', '!org.junit,!junit.framework,!org.mockito.*,*'
        instruction 'Fragment-Host', 'com.netflix.rxjava.core'
    }
}